# Maintainer: John Baier <ebusd@ebusd.eu>
# Contributor: Milan Knizek <knizek volny cz>
# Usage: makepkg -p PKGBUILD.git
pkgname=ebusd-git
_gitname=ebusd
pkgver=2.4
pkgrel=1
pkgdesc="ebusd, the daemon for communication with eBUS heating systems."
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url=https://github.com/john30/ebusd/
license=('GPL3')
depends=('setserial')
makedepends=('git')
optdepends=()
conflicts=('ebusd' 'ebusd-git')
provides=('ebusd-git')
install=ebusd.install
options=()
backup=('etc/conf.d/ebusd')
source=("git://github.com/john30/${_gitname}.git")
md5sums=('SKIP')

pkgver() {
  cd "$_gitname"
  local ver=`cat VERSION`
  local build=`git describe --always | sed 's|-|.|g'`
  echo $ver-$build|sed -e 's#-#_#g'
}

build() {
  cd "${srcdir}/${_gitname}"
  git clean -dfx
  git reset --hard
  ./autogen.sh
  make -j1
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -d                                              "${pkgdir}/usr/lib/systemd/system"
  install -m 0644 contrib/archlinux/systemd/ebusd.service "${pkgdir}/usr/lib/systemd/system"
  install -d                                              "${pkgdir}/usr/share/ebusd"                                                   
  install -m 0755 contrib/archlinux/systemd/init.sh       "${pkgdir}/usr/share/ebusd"                                                   
  install -d                                              "${pkgdir}/etc/conf.d"
  install -m 0644 contrib/archlinux/conf.d/ebusd          "${pkgdir}/etc/conf.d/ebusd"
}
