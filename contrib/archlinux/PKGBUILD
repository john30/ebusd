# Maintainer: John Baier <ebusd@ebusd.eu>
# Contributor: Milan Knizek <knizek volny cz>
# Usage: makepkg
pkgname=ebusd
pkgver=24.1
pkgrel=1
pkgdesc="ebusd, the daemon for communication with eBUS heating systems."
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url=https://github.com/john30/ebusd/
license=('GPL3')
depends=('mosquitto' 'openssl' 'ca-certificates')
makedepends=()
optdepends=()
conflicts=('ebusd' 'ebusd-git')
provides=('ebusd')
install=ebusd.install
options=()
backup=('etc/conf.d/ebusd')
source=("https://github.com/john30/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")

pkgver() {
  cat "${srcdir}/${pkgname}-${pkgver}/VERSION"|sed -e 's#-#_#g'
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./autogen.sh
  make -j1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install-strip

  install -d                                              "${pkgdir}/usr/lib/systemd/system"
  install -m 0644 contrib/archlinux/systemd/ebusd.service "${pkgdir}/usr/lib/systemd/system"
  install -d                                              "${pkgdir}/etc/conf.d"
  install -m 0644 contrib/archlinux/conf.d/ebusd          "${pkgdir}/etc/conf.d/ebusd"
  install -d                                              "${pkgdir}/etc/ebusd"
  install -m 0644 contrib/etc/ebusd/mqtt-hassio.cfg       "${pkgdir}/etc/ebusd/mqtt-hassio.cfg"
  install -m 0644 contrib/etc/ebusd/mqtt-integration.cfg  "${pkgdir}/etc/ebusd/mqtt-integration.cfg"
}
# update md5sums: updpkgsums
md5sums=('ec614447defed756e586fdf4fd73647a')
